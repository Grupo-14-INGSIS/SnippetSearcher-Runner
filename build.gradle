plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.0.21'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.jetbrains.kotlin.plugin.jpa' version '2.0.21'
    id 'org.jlleitschuh.gradle.ktlint' version '12.1.0'
    id 'jacoco'
    id 'maven-publish'
}

group = 'com.example'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

def dotenvFile = file("$rootDir/.env")
def dotenvProps = new Properties()
if (dotenvFile.exists()) {
    dotenvFile.withReader { reader ->
        reader.eachLine { line ->
            line = line.trim()
            if (!line.startsWith("#") && line.contains("=")) {
                def (key, value) = line.split("=", 2)
                dotenvProps.setProperty(key.trim(), value.trim())
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'GitHubPackages'
        url = uri("https://maven.pkg.github.com/Grupo-14-INGSIS/PrintScript-Tools")
        credentials {
            username = System.getenv("GITHUB_USERNAME") ?: dotenvProps.getProperty("GITHUB_USERNAME") ?: ""
            password = System.getenv("GITHUB_TOKEN") ?: dotenvProps.getProperty("GITHUB_TOKEN") ?: ""
        }
    }
}

dependencies {
    implementation 'IngsisTP:runner:1.0.86'

    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'
    implementation 'com.newrelic.agent.java:newrelic-api:8.7.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"
    implementation "org.jetbrains.kotlinx:kotlinx-html-jvm:0.8.0"
    implementation 'io.projectreactor.kotlin:reactor-kotlin-extensions'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5"
    testImplementation "io.mockk:mockk:1.13.10"
    testImplementation "org.junit.jupiter:junit-jupiter:5.9.3"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

springBoot {
    mainClass = 'com.grupo14IngSis.snippetSearcherRunner.ApplicationKt'
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll('-Xjsr305=strict')
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit { minimum = 0.8 }
        }
    }
}